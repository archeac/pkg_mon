#!/usr/bin/env perl
use Mojolicious::Lite;
use Mojo::IOLoop;
use Mojo::JSON qw/encode_json/;

use PkgMonitor;

my $pkgmonitor = PkgMonitor->new( log => app->log );

get '/' => sub {
    my $self = shift;

    my $packages = $pkgmonitor->pkglist;

    $self->render( template => 'index', packages => $packages );
};

get '/pkg_state' => sub {
    my $self = shift;
    $self->res->headers->content_type('text/event-stream');
    my $io_loop = Mojo::IOLoop->recurring(
        5 => sub {
            my $state_changes = $pkgmonitor->pkg_state;
            my $packages = encode_json($state_changes);
            $self->write("event:pkg_state\ndata: $packages\n\n");
        }
    );
    $self->on( finish => sub { Mojo::IOLoop->remove($io_loop) } );
};

app->start;
